\section{Definition of CW complexes}

\todo[plan]{
In this section I should first describe mathematically what a CW complex is. 
Then I show the Lean definition and explain why/how
Maybe this can also have proofs of basic lemmas (e.g. a CW complex is closed in its ambient space)}

A \emph{CW complex} is a topological space that can be constructed by glueing images of closed disc of different dimensions together along the images of their boundaries. 
These images of closed discs in the CW complex are called \emph{cells}.
To specify that a cell is the image of an $n$-dimensional disc, one can call it an $n$-cell.
The cells up to dimension $n$ make up what is called the \emph{$n$-skeleton}.
In a relative CW complex these discs can additionally be attached to a specified base set. 

The different definitions of CW complexes present in the literature can be broadly categorized into to approaches: firstly there is the ``classical'' approach that sticks closely in style to Whitehead's original definition in \cite{Whitehead2018}.
This definition assumes the cells to all lie in one topological space and then describes how the cells interact with each other and the space.
Secondly, there is a popular approach that is more categorical in nature. 
In this approach the skeletons are regarded as different spaces and the definition describes how to construct the $n+1$-skeleton from the $n$-skeleton. 
The CW complex is then defined as the colimit of the skeletons. 

At the start of this project neither of the approaches had been formalized in Lean. 
The authors chose to proceed with the former approach because it avoids having to deal with different topological spaces and inclusions between them. 
At the time of writing both approaches have been formalized and are part of \mathlib.

The definition chosen for formalization is the following: 

\begin{defi}
    Let $X$ be a Hausdorff space and $D \subseteq X$ be a subset of $X$. 
    A (relative) CW complex on $X$ consists of a family of indexing sets $(I_n)_{n \in \bN}$ and a family of continuous maps $(Q_i^n \colon D_i^n \to X)_{n \in \bN, i \in I_n}$ called \emph{characteristic maps} with the following properties: 
    \begin{enumerate}
        \item $\restrict{Q_i^n}{\interior{D_i^n}} \colon \interior{D_i^n} \to Q_i^n(\interior{D_i^n})$ is a homeomorphism for every $n \in \bN$ and $i \in I_n$. We call $\openCell{n}{i} \coloneq Q_i^n(\interior{D_i^n})$ an \emph{(open) $n$-cell} and $\closedCell{n}{i} \coloneq Q_i^n(D_i^n)$ a \emph{closed $n$-cell}.
        \item Two different open cells are disjoint.
        \item Every open cell is disjoint with $D$.
        \item For each $n \in \bN$ and $i \in I_n$ the \emph{cell frontier} $\cellFrontier{n}{i} \coloneq Q_i^n(\boundary D_i^n)$ is contained in the union of $D$ with a finite number of closed cells of a lower dimension.
        \item A set $A \subseteq X$ is closed if $A \cap D$ and the intersections $A \cap \closedCell{n}{i}$ are closed for all $n \in \bN$ and $i \in I_n$.
        \item $D$ is closed. 
        \item The union of $D$ and all closed cells is $X$.
    \end{enumerate}
\end{defi}

It is important to notice that an open cell is not necessarily open and that the cell frontier is not necessarily the frontier of the corresponding cell.

In \mathlib this definition translates to the following:
 
\begin{lstlisting}
    class RelCWComplex.{u} {X : Type u} [TopologicalSpace X] (C : Set X) 
        (D : outParam (Set X)) where
  cell (n : ℕ) : Type u
  map (n : ℕ) (i : cell n) : PartialEquiv (Fin n → ℝ) X
  source_eq (n : ℕ) (i : cell n) : (map n i).source = ball 0 1
  continuousOn (n : ℕ) (i : cell n) : ContinuousOn (map n i) (closedBall 0 1)
  continuousOn_symm (n : ℕ) (i : cell n) : ContinuousOn (map n i).symm 
    (map n i).target
  pairwiseDisjoint' :
    (univ : Set (Σ n, cell n)).PairwiseDisjoint 
    (fun ni ↦ map ni.1 ni.2 '' ball 0 1)
  disjointBase' (n : ℕ) (i : cell n) : Disjoint (map n i '' ball 0 1) D
  mapsTo (n : ℕ) (i : cell n) : ∃ I : Π m, Finset (cell m),
    MapsTo (map n i) (sphere 0 1) 
    (D ∪ ⋃ (m < n) (j ∈ I m), map m j '' closedBall 0 1)
  closed' (A : Set X) (hAC : A ⊆ C) :
    ((∀ n j, IsClosed (A ∩ map n j '' closedBall 0 1)) ∧ IsClosed (A ∩ D)) → IsClosed A
  isClosedBase : IsClosed D
  union' : D ∪ ⋃ (n : ℕ) (j : cell n), map n j '' closedBall 0 1 = C
\end{lstlisting}

One obvious change in the Lean definition is that instead of talking about the topological space $X$ being a CW complex, it talks about the set $C$ being a CW complex in the ambient space $X$.
This eases working with constructions and examples of CW complexes. 
For constructions it allows you to avoid dealing with constructed topologies, for example the disjoint union topology, the product topology, etc., and for examples it allows you to use the possibly nicer topology of the ambient space that is often already naturally present. 
It is however derivable from the definition that $C$ is closed in $X$. 
So while a closed interval in the real line can be considered as a CW complex in the natural ambient space that is the real line, the open interval cannot and needs to be considered as a CW complex in itself. 
This approach is inspired by \cite{Gonthier2013}, where the authors notice that it is helpful to consider subsets of an ambient group to avoid having to work with different group operations and similar issues.

Even though the behaviour of a CW complex depends strongly on its data and there can be different ``non-equivalent'' CW complex structures on the same space, we have chosen to make it a \lstinline|class|, effectively treating it more like a property than a structure. 
This is to be able to make use of Lean's typeclass inference. 
\todo[comment]{Is this too basic?}

We omit the requirement for $X$ to be a Hausdorff space and instead naturally require it for most of the lemmata. 