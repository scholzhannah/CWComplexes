\section{Definition of CW complexes}

An \emph{(absolute) CW complex} is a topological space that can be constructed by glueing images of closed discs of different dimensions together along the images of their boundaries. 
The image of an n-dimensional open disc in the CW complex is called an \emph{$n$-cell}.
The cells up to and including dimension $n$ make up what is called the \emph{$n$-skeleton}.
In a \emph{relative CW complex} these discs can additionally be attached to a specified base set. 

Definitions of CW complexes present in the literature can be broadly categorized into two approaches: firstly, there is the ``classical'' approach that sticks closely in style to \citeauthor{Whitehead2018}s original definition in \cite{Whitehead2018}.
This definition assumes that the cells all lie in one topological space and describes how they interact with each other and the topology.
The second approach is more categorical in nature. 
In this version, the $(n + 1)$-skeleton is defined as a pushout involving the $n$-skeleton. 
The CW complex is then defined as the colimit of the skeletons. 

At the start of this project neither of the approaches had been formalized in Lean. 
We chose to proceed with the former approach because it avoids having to deal with different topological spaces and inclusions between them. 
As the other approach has been formalized by Jiazhen Xia and Elliot Dean Young and refactored by Joël Riou, both are now formalized and part of \mathlib. 
Our version is called \lstinline|Topology.RelCWComplex|, while the other one is \lstinline|TopCat.RelativeCWComplex|.

The definition chosen for formalization is the following, where $D^n$ is the closed unit $n$-disc:

\begin{defi}\label{def:cwcomplex}
    Let $X$ be a Hausdorff space and $D \subseteq X$ be a subset of $X$. 
    A \emph{(relative) CW complex} on $X$ with \emph{base} $D$ consists of a family of indexing sets $(I_n)_{n \in \bN}$ and a family of continuous maps $(Q_i^n \colon D^n \to X)_{n \in \bN, i \in I_n}$ called \emph{characteristic maps} with the following properties: 
    \begin{enumerate}
        \item $\restrict{Q_i^n}{\interior{D^n}} \colon \interior{D^n} \to Q_i^n(\interior{D^n})$ is a homeomorphism for every $n \in \bN$ and $i \in I_n$. We call $\openCell{n}{i} \coloneq Q_i^n(\interior{D^n})$ an \emph{(open) $n$-cell} and $\closedCell{n}{i} \coloneq Q_i^n(D^n)$ a \emph{closed $n$-cell}.
        \item Two different open cells are disjoint.
        \item Every open cell is disjoint with $D$.
        \item For each $n \in \bN$ and $i \in I_n$ the \emph{cell frontier} $\cellFrontier{n}{i} \coloneq Q_i^n(\boundary D^n)$ is contained in the union of $D$ with a finite number of closed cells of a lower dimension.
        \item A set $A \subseteq X$ is closed if $A \cap D$ and the intersections $A \cap \closedCell{n}{i}$ are closed for all $n \in \bN$ and $i \in I_n$. We say that $X$ has \emph{weak topology} with respect to the closed cells. 
        \item $D$ is closed. 
        \item The union of $D$ and all closed cells is $X$.
    \end{enumerate}
\end{defi}

It is important to notice that an open cell is not necessarily open and that the cell frontier is not necessarily the frontier of the corresponding cell.

The translation of this definition in \mathlib can be found in Figure \ref{fig:def}.

\begin{figure}
\caption{Definition of relative CW complexes in \mathlib}
\label{fig:def}
\begin{lstlisting}[frame=single]
class RelCWComplex.{u} {X : Type u} [TopologicalSpace X] (C : Set X) 
        (D : outParam (Set X)) where
  cell (n : ℕ) : Type u
  map (n : ℕ) (i : cell n) : PartialEquiv (Fin n → ℝ) X
  source_eq (n : ℕ) (i : cell n) : (map n i).source = ball 0 1
  continuousOn (n : ℕ) (i : cell n) : ContinuousOn (map n i) (closedBall 0 1)
  continuousOn_symm (n : ℕ) (i : cell n) : ContinuousOn (map n i).symm 
    (map n i).target
  pairwiseDisjoint' :
    (univ : Set (Σ n, cell n)).PairwiseDisjoint 
    (fun ni ↦ map ni.1 ni.2 '' ball 0 1)
  disjointBase' (n : ℕ) (i : cell n) : Disjoint (map n i '' ball 0 1) D
  mapsTo (n : ℕ) (i : cell n) : ∃ I : Π m, Finset (cell m),
    MapsTo (map n i) (sphere 0 1) 
    (D ∪ ⋃ (m < n) (j ∈ I m), map m j '' closedBall 0 1)
  closed' (A : Set X) (hAC : A ⊆ C) :
    ((∀ n j, IsClosed (A ∩ map n j '' closedBall 0 1)) ∧ IsClosed (A ∩ D)) → IsClosed A
  isClosedBase : IsClosed D
  union' : D ∪ ⋃ (n : ℕ) (j : cell n), map n j '' closedBall 0 1 = C
\end{lstlisting}
\end{figure}

One obvious change in the Lean definition is that instead of talking about the topological space $X$ being a CW complex, it talks about a set $C$ being a CW complex in the ambient space $X$.
This allows us to treat spaces that are naturally defined as subspaces of a given space as a CW complex without taking subtypes. 
Additionally, for constructions such as the disjoint union of two CW complexes, it avoids dealing with constructed topologies.
It is however derivable from the definition that $C$ is closed in $X$. 
So while a closed interval in the real line can be considered as a CW complex in its natural ambient space, the open interval cannot and needs to be considered as a CW complex in itself. 
This approach is inspired by \cite{Gonthier2013}, where the authors notice that it is helpful to consider subsets of an ambient group to avoid having to work with different group operations and similar issues.

Even though the behaviour of a CW complex depends strongly on its data and there can be different ``non-equivalent''\footnote{in the sense of cellular isomorphism} CW structures on the same space, we have chosen to make it a \lstinline|class|, effectively treating it more like a property than a structure. 
This is to be able to make use of Lean's typeclass inference (see Section \ref{sub:leanandmathlib}).

We don't require $X$ to be a Hausdorff space, so to properly state that $C$ is a CW complex with base $D$, we write \lstinline|[RelCWComplex C D] [T2Space X]|.

The base $D$ is an \lstinline|outParam| (see Section \ref{sub:leanandmathlib}). 
This is because lemma statements about CW complexes typically refer to just the underlying set \lstinline|C| without mentioning the base \lstinline|D|. 
Normally, for typeclass inference to run the user would have to go out of their way to specify \lstinline|D|. 
We disable this requirement by adding the \lstinline|outParam| specification.

In topology, most CW complexes that are considered have empty base and often the term ``CW complex'' refers to this type of complex. 
Those CW complexes are called \emph{absolute CW complexes}. 

Most naturally one would simply define absolute CW complexes in Lean in the same way: as a relative CW complex with empty base.
However, this leads to two issues: 
firstly, when defining an absolute CW complex there are now trivial proofs that need to be provided and some simplifications that need to be performed for every new instance and definition. 
This produces a lot of duplicate code or requires a separate definition that is used as a replacement constructor. 
Secondly, with absolute CW complexes we have encountered instances on the same set with provably but not definitionally equal base sets. 
The product of two CW complexes \lstinline|RelCWComplex C D| and \lstinline|RelCWComplex E F| is of type \lstinline|RelCWComplex (C ×ˢ E) (D ×ˢ E ∪ C ×ˢ F)| where \lstinline|×ˢ| is the binary product of sets. 
For absolute CW complexes we get \lstinline|RelCWComplex (C ×ˢ E) (∅ ×ˢ E ∪ C ×ˢ ∅)| which is not definitionally equal to \lstinline|RelCWComplex (C ×ˢ E) ∅|.
For this reason, we define an instance specifically for absolute CW complexes and want this to be inferred over the relative version. 
But since $D$ is an \lstinline|outParam|, we cannot specify typeclass inference to be looking for a base that is definitionally equal to the empty set. 

The solution is to have absolute CW complexes be their own class that agrees with relative CW complexes except for the empty base, trivial proofs and simplifications. 
The type of absolute CW complexes on the set $C$ in Lean is \lstinline|CWComplex C|. 
We then provide an instance stating that absolute CW complexes are relative CW complexes and a definition in the other direction for relative CW complexes with empty base.
The latter cannot be an instance as this would create an instance loop.
Additionally, it would enable typeclass inference to also consider \lstinline|RelCWComplex| instances when looking for a \lstinline|CWComplex| instance, which is exactly what we wanted to avoid.
To avoid having duplicate notions \lstinline|CWComplex.cell| and \lstinline|RelCWComplex.cell| and \lstinline|CWComplex.map| and \lstinline|RelCWComplex.map|, we mark the version for absolute CW complexes as \lstinline|protected| strongly encouraging the user to only use the version for relative CW complexes which is also available for absolute ones through the instance.\todo{Explain protected above}
\todo[plan]{Talk about attribute when it is done}

As in Definition \ref{def:cwcomplex}, we define the notions of open cells, closed cells and cell frontiers.
We define them only for relative CW complexes but, as for the indexing types and characteristic maps, these notions can be used for absolute ones because of the instance mentioned above. 

We then define subcomplexes as closed sets that are unions of open cells of the complex.

\begin{lstlisting}[frame=single]
structure Subcomplex (C : Set X) {D : Set X} [RelCWComplex C D] where
  carrier : Set X
  I : Π n, Set (cell C n)
  closed' : IsClosed carrier
  union' : D ∪ ⋃ (n : ℕ) (j : I n), openCell (C := C) n j = carrier
\end{lstlisting}

We provide additional definitions for other ways of describing them: firstly, as a union of open cells where the closure of every cell is already contained in the union and secondly, as a union of open cells that is also a CW complex.
Here is the former as we will need it below:

\begin{lstlisting}[frame=single]
def RelCWComplex.Subcomplex.mk' [T2Space X] (C : Set X) {D : Set X} 
    [RelCWComplex C D] (E : Set X) (I : Π n, Set (cell C n))
    (closedCell_subset : ∀ (n : ℕ) (i : I n), closedCell (C := C) n i ⊆ E)
    (union : D ∪ ⋃ (n : ℕ) (j : I n), openCell (C := C) n j = E) : 
    Subcomplex C where
  carrier := E
  I := I
  closed' := /- Proof omitted-/
  union' := union
\end{lstlisting}

We show that subcomplexes are again CW complexes and that the type of subcomplexes of a specific CW complex has the structure of a \lstinline|CompletelyDistribLattice| (see Section  \ref{sub:mathinlean}).  

Defining subcomplexes allows us to talk about the skeletons of a CW complex. 
The typical definition of the $n$-skeleton in the following: 

\begin{defi}
  The $n$-skeleton of a CW complex $C$ is defined as $C_n \coloneq \bigcup_{m < n + 1} \bigcup_{i \in I_m} \closedCell{m}{i}$ where $-1 \le n \le \infty$.
\end{defi}

Since proofs about CW complexes frequently employ induction, we want to make using this proof technique as easy as possible. 
Starting an induction at $-1$ is unfortunately not that convenient in Lean. 
For this reason, we first define an auxiliary version of the skeletons where the dimensions are shifted by one: 

\begin{lstlisting}[frame=single]
def RelCWComplex.skeletonLT (C : Set X) {D : Set X} [RelCWComplex C D] 
    (n : ℕ∞) : Subcomplex C :=
  Subcomplex.mk' _ (D ∪ ⋃ (m : ℕ) (_ : m < n) (j : cell C m), closedCell m j)
    (fun l ↦ {x : cell C l | l < n}) (/-Proof omitted-/) (/-Proof omitted-/)
\end{lstlisting}

We use this to define the usual skeleton: 

\begin{lstlisting}[frame=single]
abbrev RelCWComplex.skeleton (C : Set X) {D : Set X} [RelCWComplex C D] 
    (n : ℕ∞) : Subcomplex C :=
  skeletonLT C (n + 1)
\end{lstlisting}

Since we expect proofs about \lstinline|skeleton| to be short reductions of the claim to the corresponding statement about \lstinline|skeletonLT|, we spare the user the manual unfolding of \lstinline|skeleton| by marking it as an \lstinline|abbrev| instead of a \lstinline|def| (see Section \ref{sub:leanandmathlib}).
The definition \lstinline|skeleton| exists mostly for completeness' sake. 
Both lemmata and definitions should use \lstinline|skeletonLT| to make proofs easier and then possibly derive a version for \lstinline|skeleton|.

\todo[comment, inline]{Should subcomplexes and cellular maps go into a seperate section? They don't really fit here but  also think there isn't enough to say to put them in their own section.}

We also want to introduce a sensible notion of structure preserving maps between CW complexes.
A natural notion is a \emph{cellular map}. 
A cellular map is a continuous map between two CW complexes $X$ and $Y$ that sends the $n$-skeleton of $X$ to the $n$-skeleton of $Y$ for every $n$.
In Lean this definition translates to: 

\begin{lstlisting}[frame=single]
structure CellularMap (C : Set X) {D : Set X} [RelCWComplex C D] (E : Set Y) 
    {F : Set Y} [RelCWComplex E F] where
  protected toFun : X → Y
  protected continuousOn_toFun : ContinuousOn toFun C
  image_skeletonLT_subset' (n : ℕ) : toFun '' (skeletonLT C n) ⊆ skeletonLT E n
\end{lstlisting}

We also introduce the notion of \emph{cellular isomorphisms}: \todo{Is there even a math name?}

\begin{lstlisting}[frame=single]
structure CellularEquiv (C : Set X) {D : Set X} [RelCWComplex C D] (E : Set Y) 
    {F : Set Y} [RelCWComplex E F] extends PartialEquiv X Y where
  continuousOn_toPartialEquiv : ContinuousOn toPartialEquiv C
  image_toPartialEquiv_skeletonLT_subset' (n : ℕ) :
    toPartialEquiv '' (skeletonLT C n) ⊆ skeletonLT E n
  continuousOn_toPartialEquiv_symm : ContinuousOn toPartialEquiv.symm E
  image_topPartialEquiv_symm_skeletonLT_subset' (n : ℕ) :
    toPartialEquiv.symm '' (skeletonLT E n) ⊆ skeletonLT C n
  source_eq : toPartialEquiv.source = C
  target_eq : toPartialEquiv.target = E
\end{lstlisting}

\todo[plan]{Mention cellular approximation here?}